apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

// fast build if enable multi dex, default is 16
int minSdk = hasProperty('devMinSdk') ? devMinSdk.toInteger() : 16

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolVersion

    defaultConfig {
        applicationId "com.github.khangnt.ymusic"
        minSdkVersion minSdk
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.buildNumber()
        versionName "0.0.${versionCode}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '.debug'
        }
    }

    retrolambda {
        javaVersion JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':heterogeneousadapter')

    // Android support libraries
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:support-v4:${rootProject.ext.supportVersion}"

    // Image loader - Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // DI - Dagger 2
    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    // Logging - Timber
    compile 'com.jakewharton.timber:timber:4.4.0'

    // Concurrency - Rx Java 2
    compile 'io.reactivex.rxjava2:rxjava:2.0.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.0.1'
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.0.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'

    // Networking Retrofit2
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Test compile
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testCompile 'junit:junit:4.12'
}
